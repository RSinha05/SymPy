ADD: "+"
SUB: "-"
MUL: "*"
DIV: "/"

L_PAREN: "("
R_PAREN: ")"
L_BRACE: "{"
R_BRACE: "}"
L_BRACKET: "["
R_BRACKET: "]"

BAR: "|"

// FUNC_LIM:  "\\lim"
// LIM_APPROACH_SYM: "\\to" | "\\rightarrow" | "\\Rightarrow" | "\\longrightarrow" | "\\Longrightarrow"
// FUNC_INT:  "\\int"
// FUNC_SUM:  "\\sum"
// FUNC_PROD: "\\prod"

FUNC_LOG:  "\\log"
FUNC_LN:   "\\ln"
FUNC_SIN:  "\\sin"
FUNC_COS:  "\\cos"
FUNC_TAN:  "\\tan"
FUNC_CSC:  "\\csc"
FUNC_SEC:  "\\sec"
FUNC_COT:  "\\cot"

FUNC_ARCSIN: "\\arcsin"
FUNC_ARCCOS: "\\arccos"
FUNC_ARCTAN: "\\arctan"
FUNC_ARCCSC: "\\arccsc"
FUNC_ARCSEC: "\\arcsec"
FUNC_ARCCOT: "\\arccot"

FUNC_SINH: "\\sinh"
FUNC_COSH: "\\cosh"
FUNC_TANH: "\\tanh"
FUNC_ARSINH: "\\arsinh"
FUNC_ARCOSH: "\\arcosh"
FUNC_ARTANH: "\\artanh"

// FUNC_SQRT: "\\sqrt"

CMD_TIMES: "\\times"
CMD_CDOT:  "\\cdot"
CMD_DIV:   "\\div"
// CMD_FRAC:  "\\frac"
//
// CMD_MATHIT: "\\mathit"
//
// UNDERSCORE: "_"
CARET: "^"
// COLON: ":"

SYMBOL: /(\\)?[a-zA-Z]+/
INTEGER: /[+-]?\d+/
FLOAT: /[+-]?\d+\.\d*|[+-]?\.\d+/

EQUAL: "="
LT: "<"
LTE: "\\leq"
GT: ">"
GTE: "\\geq"

// BANG: "!"

?start: relation

relation: expr EQUAL expr
        | expr LT expr
        | expr LTE expr
        | expr GT expr
        | expr GTE expr
        | expr

?expr: func
     | binary_op

?binary_op: binary_op_1
          | expr ADD binary_op_1
          | expr SUB binary_op_1

?binary_op_1: binary_op_2
            | binary_op_1 MUL binary_op_2
            | binary_op_1 CMD_TIMES binary_op_2
            | binary_op_1 CMD_CDOT binary_op_2
            | binary_op_1 DIV binary_op_2
            | binary_op_1 CMD_DIV binary_op_2

?binary_op_2: part
            | group
            | unary_op
            | binary_op_2 CARET part
            | binary_op_2 CARET group
            | binary_op_2 CARET unary_op

?unary_op: SUB group
         | SUB part

?group: abs_group
     | L_PAREN expr R_PAREN
     | L_BRACE expr R_BRACE
     | L_BRACKET expr R_BRACKET

?abs_group: BAR expr BAR

?func_normal: FUNC_LOG
            | FUNC_LN
            | FUNC_SIN
            | FUNC_COS
            | FUNC_TAN
            | FUNC_CSC
            | FUNC_SEC
            | FUNC_COT
            | FUNC_ARCSIN
            | FUNC_ARCCOS
            | FUNC_ARCTAN
            | FUNC_ARCCSC
            | FUNC_ARCSEC
            | FUNC_ARCCOT
            | FUNC_SINH
            | FUNC_COSH
            | FUNC_TANH
            | FUNC_ARSINH
            | FUNC_ARCOSH
            | FUNC_ARTANH

?func: func_normal L_PAREN func_arg? R_PAREN

?func_arg: expr
         | expr "," func_arg

?part: SYMBOL
     | FLOAT
     | INTEGER


%ignore /[ \t\n\r]+/

%ignore /\\,|\\thinspace/
%ignore /\\:|\\medspace/
%ignore /\\;|\\thickspace/
%ignore "\\quad"
%ignore "\\qquad"
%ignore /\\!|\\negthinspace/
%ignore "\\negmedspace"
%ignore "\\negthickspace"

%ignore "\\left"
%ignore "\\right"

// need logix for a b meaning a x b